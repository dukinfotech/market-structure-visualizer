// @version=5
indicator("MACD-based Market Structure Visualizer", overlay=true, max_bars_back = 500) // Tăng max_bars_back


// Getting inputs
histogramMinLen = input.int(title = "Histogram Min Length", minval=1, defval=2)
fastLen = input(title = "Fast Length", defval = 12)
slowLen = input(title = "Slow Length", defval = 26)
src = input(title = "Source", defval = close, display = display.data_window)
signalLen = input.int(title = "Signal Smoothing",  minval = 1, maxval = 50, defval = 9)
sma_source = input.string(title = "Oscillator MA Type",  defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)
sma_signal = input.string(title = "Signal Line MA Type", defval = "EMA", options = ["SMA", "EMA"], display = display.data_window)

// Calculating
fastMA = sma_source == "SMA" ? ta.sma(src, fastLen) : ta.ema(src, fastLen)
slowMA = sma_source == "SMA" ? ta.sma(src, slowLen) : ta.ema(src, slowLen)
macd = fastMA - slowMA
signal = sma_signal == "SMA" ? ta.sma(macd, signalLen) : ta.ema(macd, signalLen)
histogram = macd - signal

const int LINE_WIDTH = 2
const int LABEL_GAP = 0


// --- BOS, CHOCHH LOGIC ---
var float lastExtremeHigh = na // Biến để lưu trữ đỉnh quan trọng cuối cùng
var float lastExtremeLow = na  // Biến để lưu trữ đáy quan trọng cuối cùng
var int lastExtremeHighBarIndex = na // Biến để lưu trữ chỉ mục nến có đỉnh quan trọng cuối cùng
var int lastExtremeLowBarIndex = na  // Biến để lưu trữ chỉ mục nến có đáy quan trọng cuối cùng
var bool isLastBoS = false // Biến để theo dõi xem BoS đã xảy ra chưa
var float prevDowntrendExtremeHigh = na // Biến để lưu trữ đỉnh quan trọng cuối cùng của xu hướng giảm trước đó
var float prevUptrendExtremeLow = na // Biến để lưu trữ đáy quan trọng cuối cùng của xu hướng tăng trước đó
var int prevDowntrendExtremeHighBarIndex = na // Biến để lưu trữ chỉ mục nến có đỉnh quan trọng cuối cùng của xu hướng giảm trước đó
var int prevUptrendExtremeLowBarIndex = na // Biến để lưu trữ chỉ mục nến có đáy quan trọng cuối cùng của xu hướng tăng trước đó

// --- BOS LOGIC ---
// Kiểm tra nếu giá đóng cửa vượt qua giá cao nhất cuối cùng
if not na (lastExtremeHigh) and close >= lastExtremeHigh and isLastBoS == false
    isLastBoS := true
    line.new(lastExtremeHighBarIndex, lastExtremeHigh, bar_index, lastExtremeHigh, color=color.blue, style=line.style_dashed, width=LINE_WIDTH)
    label.new(bar_index - LABEL_GAP, lastExtremeHigh + LABEL_GAP, text="BoS", color=color.blue, style=label.style_label_down, textcolor=color.white, size=size.small)

    prevUptrendExtremeLow := low
    for i = 1 to bar_index - lastExtremeHighBarIndex
        if low[i] < prevUptrendExtremeLow   
            prevUptrendExtremeLow := low[i]
            prevUptrendExtremeLowBarIndex := bar_index - i
    
// Kiểm tra nếu giá đóng cửa vượt qua giá thấp nhất cuối cùng
if not na (lastExtremeLow) and close <= lastExtremeLow and isLastBoS == false
    isLastBoS := true
    line.new(lastExtremeLowBarIndex, lastExtremeLow, bar_index, lastExtremeLow, color=color.blue, style=line.style_dashed, width=LINE_WIDTH)
    label.new(bar_index - LABEL_GAP, lastExtremeLow - LABEL_GAP, text="BoS", color=color.blue, style=label.style_label_up, textcolor=color.white, size=size.small)
    
    prevDowntrendExtremeHigh := high
    for i = 1 to bar_index - lastExtremeLowBarIndex
        if high[i] < prevDowntrendExtremeHigh   
            prevDowntrendExtremeHigh := high[i]
            prevDowntrendExtremeHighBarIndex := bar_index - i

// --- CHoCH LOGIC ---
// Kiểm tra nếu giá đóng cửa vượt qua giá thấp nhất của xu hướng tăng trước đó
if not na (prevUptrendExtremeLow) and close <= prevUptrendExtremeLow
    line.new(prevUptrendExtremeLowBarIndex, prevUptrendExtremeLow, bar_index, prevUptrendExtremeLow, color=color.orange, style=line.style_dashed, width=LINE_WIDTH)
    label.new(bar_index - LABEL_GAP, prevUptrendExtremeLow - LABEL_GAP, text="CHoCH", color=color.orange, style=label.style_label_up, textcolor=color.white, size=size.small)
    prevUptrendExtremeLow := na // Reset sau khi đã đánh dấu ChoCH

// Kiểm tra nếu giá đóng cửa vượt qua giá cao nhất của xu hướng giảm trước đó
if not na (prevDowntrendExtremeHigh) and close >= prevDowntrendExtremeHigh
    line.new(prevDowntrendExtremeHighBarIndex, prevDowntrendExtremeHigh, bar_index, prevDowntrendExtremeHigh, color=color.orange, style=line.style_dashed, width=LINE_WIDTH)
    label.new(bar_index - LABEL_GAP, prevDowntrendExtremeHigh + LABEL_GAP, text="CHoCH", color=color.orange, style=label.style_label_down, textcolor=color.white, size=size.small)
    prevDowntrendExtremeHigh := na // Reset sau khi đã đánh dấu ChoCH


// --- SWING/CROSS POINT LOGIC ---
var int[] reversalCandleStickIdxs = array.new_int() // Biến để lưu trữ chỉ mục các nến xảy ra cắt nhau
var line marketStructureLine = na // Biến để lưu trữ đường vẽ cấu trúc thị trường

// Hàm kiểm tra xem có tín hiệu cắt nhau được xác nhận không dựa trên quy tắc số lượng thanh histogram mỗi con sóng
isConfirmedReversalMarket() =>
    bool isConfirmed = barstate.isconfirmed

    for i = 0 to histogramMinLen * 2 - 2
        if i != (histogramMinLen - 1)
            if histogram[i] * histogram[i + 1] > 0
                isConfirmed := isConfirmed and true
            else
                isConfirmed := isConfirmed and false
        else
            if histogram[i] * histogram[i + 1] < 0
                isConfirmed := isConfirmed and true
            else
                isConfirmed := isConfirmed and false
        
    isConfirmed

// Hàm lấy đỉnh/đáy của swing bất kỳ
getSwingExtremeCandleSticks(startReversalCandleStickIdx, endReversalCandleStickIdx) =>
    float swingExtremePrice = 0
    int swingExtremeIdx = 0

    int offsetStart = 0 // Bắt đầu từ nến gần nhất của swing
    int offsetEnd = endReversalCandleStickIdx - startReversalCandleStickIdx // Kết thúc tại nến xa nhất của swing

    bool isBearSwing = histogram[offsetEnd] < 0

    // Duyệt các offset từ gần tới xa (offStart -> offEnd)
    for i = offsetStart to offsetEnd
        if isBearSwing
            if low[i] < swingExtremePrice or swingExtremePrice == 0
                swingExtremePrice := low[i]
                swingExtremeIdx := (endReversalCandleStickIdx + 2) - i
        else
            if high[i] > swingExtremePrice or swingExtremePrice == 0
                swingExtremePrice := high[i]
                swingExtremeIdx := (endReversalCandleStickIdx + 2) - i

    [swingExtremeIdx, swingExtremePrice]



// -------------------- MAIN FUNCTION --------------------
// Gọi hàm main mỗi khi có nến mới
bool isConfirmedReversalMarket = isConfirmedReversalMarket()
// Khi có tín hiệu cắt được xác nhận dựa trên quy tắc số lượng thanh histogram mỗi con sóng
if isConfirmedReversalMarket

    // Chỉ mục của cây nến xảy ra tín hiệu cắt
    int reversalCandleStickIdx = bar_index - histogramMinLen
    
    // Lưu chỉ mục nến xảy ra tín hiệu cắt vào mảng
    array.push(reversalCandleStickIdxs, reversalCandleStickIdx)
    
    // Lấy kích thước mảng chỉ mục nến xảy ra tín hiệu cắt
    reversalCandleStickSize = array.size(reversalCandleStickIdxs)

    // Chỉ tính toán giá cao nhất, thấp nhất và vẽ khi có ít nhất 2 điểm cắt trở lên (trong đó có 1 swing hoàn chỉnh)
    if reversalCandleStickSize >= 2
        // Lấy chỉ mục nến của điểm cắt hiện tại và điểm cắt trước đó
        int currentReversalCandleStickIdx = array.get(reversalCandleStickIdxs, reversalCandleStickSize - 1)
        int lastReversalCandleStickIdx = array.get(reversalCandleStickIdxs, reversalCandleStickSize - 2)

        // Lấy đỉnh/đáy của swing vừa hoàn thành
        [swingExtremeIdx, swingExtremePrice] = getSwingExtremeCandleSticks(lastReversalCandleStickIdx, currentReversalCandleStickIdx)

        // Vẽ đỉnh/đáy swing lên biểu đồ
        color labelColor = histogram[histogramMinLen] < 0 ? color.red : color.green
        string labelStyle = histogram[histogramMinLen] < 0 ? label.style_label_up : label.style_label_down
        label.new(swingExtremeIdx, swingExtremePrice, text=str.tostring(swingExtremePrice), color=labelColor, style=labelStyle, textcolor=color.white, size=size.normal)

        // Lưu trữ điểm Peak A (điểm High/Low mới tìm được)
        float peak_A_price = swingExtremePrice

        // Lấy chỉ mục nến của Peak A
        int peak_A_index = swingExtremeIdx
        
        // Vẽ đường nối các đỉnh/đáy swing
        if not na(marketStructureLine)
            // Lấy tọa độ cuối cùng của đường cũ (Peak B)
            int peak_B_index = line.get_x2(marketStructureLine)
            float peak_B_price = line.get_y2(marketStructureLine)

            // Vẽ đường mới nối Peak B (điểm cuối của đường cũ) với Peak A (điểm High mới tìm được)
            line.new(peak_B_index, peak_B_price, peak_A_index, peak_A_price, xloc=xloc.bar_index, style=line.style_solid, width=3, color=color.new(color.blue, 0))
            
            // Cần cập nhật line.new() cuối cùng để lần sau lấy điểm cuối
            line.set_xy2(marketStructureLine, peak_A_index, peak_A_price) 
        else
            // Lần chạy đầu tiên, chỉ lưu Peak A để lần sau dùng làm Peak B
            marketStructureLine := line.new(peak_A_index, peak_A_price, peak_A_index, peak_A_price, xloc=xloc.bar_index, style=line.style_solid, width=3, color=color.new(color.gray, 100))